buildscript {
	repositories { jcenter() }
	dependencies { classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.4' }
}

plugins {
	id "com.jfrog.bintray" version "1.8.4"
	id 'com.github.ben-manes.versions' version '0.20.0'
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'com.github.johnrengelman.shadow'

sourceCompatibility = 1.8
targetCompatibility = 1.8
group = "org.vcsreader"
version = "1.1.3"

repositories {
	mavenLocal()
	mavenCentral()
}

dependencies {
	// Use Java5 annotations because groovy can't compile Java8 @NotNull annotations.
	compile "org.jetbrains:annotations-java5:16.0.3"
	compile "com.googlecode.juniversalchardet:juniversalchardet:1.0.3"
	testCompile "org.hamcrest:hamcrest-all:1.3"
	testCompile "junit:junit:4.12"
	testCompile "org.codehaus.groovy:groovy-all:3.0.0-alpha-4"
	testCompile "org.mockito:mockito-core:2.23.4"
}

sourceSets {
	main {
		java { srcDir "src/main" }
	}
	test {
		java { srcDir "src/test" }
		groovy { srcDir "src/test" }
	}
}

//
// Configuration for publishing vcsreader to central.sonatype.
// See http://central.sonatype.org/pages/gradle.html
//
archivesBaseName = "vcsreader"
task javadocWithLinks(type: Javadoc) {
	options {
		links = ["."]
	}
}
task javadocJar(type: Jar) {
	classifier = "javadoc"
	from javadocWithLinks
}
task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}
artifacts {
	archives javadocJar, sourcesJar, shadowJar
}

shadowJar {
	version = version
	relocate 'org.intellij', 'org.vcsreader.org.intellij'
	relocate 'org.jetbrains', 'org.vcsreader.org.jetbrains'
	relocate 'org.mozilla', 'org.vcsreader.org.mozilla'
}

publishing {
	publications {
		vcsReader(MavenPublication) {
			artifact sourcesJar
			artifact javadocJar
			from components.java
			groupId project.group
			artifactId project.name
			version project.version
		}
	}
}

bintray {
	user = System.getenv("BINTRAY_USER") ?: ""
	key = System.getenv("BINTRAY_API_KEY") ?: ""
	publications = ["vcsReader"]

	pkg {
		repo = 'code-analysis'
		name = 'vcs-reader'
		userOrg = 'arturbosch'
		licenses = ['Apache-2.0']
		vcsUrl = "https://github.com/arturbosch/vcs-reader"

		version {
			name = project.version
			released = new Date()
		}
	}
}
