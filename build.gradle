buildscript {
	repositories { jcenter() }
	dependencies { classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4' }
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.github.johnrengelman.shadow'

sourceCompatibility = 1.8
targetCompatibility = 1.8
group = "org.vcsreader"
version = "1.1.0"

repositories {
	mavenLocal()
	mavenCentral()
	// Use the following repository to fetch dependencies from 'lib' folder
	// (has to be commented out before publishing to central.sonatype)
	maven { url 'lib' }
}

dependencies {
	// Use Java5 annotations because groovy can't compile Java8 @NotNull annotations.
	compile "org.jetbrains:annotations-java5:15.0"
	compile "com.googlecode.juniversalchardet:juniversalchardet:1.0.3"
	testCompile "org.hamcrest:hamcrest-all:1.3"
	testCompile "junit:junit:4.11"
	testCompile "org.codehaus.groovy:groovy-all:2.4.6"
	testCompile "org.mockito:mockito-core:1.10.19"
}

sourceSets {
	main {
		java { srcDir "src/main" }
	}
	test {
		java { srcDir "src/test" }
		groovy { srcDir "src/test" }
	}
}

//
// Configuration for publishing vcsreader to central.sonatype.
// See http://central.sonatype.org/pages/gradle.html
//
archivesBaseName = "vcsreader"
task javadocWithLinks(type: Javadoc) {
	options {
		links = ["."]
	}
}
task javadocJar(type: Jar) {
	classifier = 'javadoc'
	from javadocWithLinks
}
task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}
artifacts {
	archives javadocJar, sourcesJar, shadowJar
}

//
// Configuration for creating single jar (with all dependant packages renamed to avoid potential conflicts).
// Can be used with `gradle install publish`, where `install` required to create src and javadoc jars
// and `publish` will create shadowed jar (see also https://github.com/johnrengelman/shadow/issues/120).
//
shadowJar {
	baseName = 'vcsreader'
	version = version
	classifier = null
	relocate 'org.intellij', 'shadow.org.intellij'
	relocate 'org.jetbrains', 'shadow.org.jetbrains'
	relocate 'org.mozilla', 'shadow.org.mozilla'
}